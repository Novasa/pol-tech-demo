plugins {
    id 'com.android.application'
    id 'kotlin-android'

    // Kotlin annotation processor plugin, must be present in all modules that use annotation processors
    id 'kotlin-kapt'

    id 'dagger.hilt.android.plugin'

    // Generates NavController action methods
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.example.facts"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        // Custom test runner to enable automated instrumentation tests with Hilt
        testInstrumentationRunner "com.example.facts.test.FactsHiltTestRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
        kotlinCompilerVersion '1.5.31'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    packagingOptions {
        // Some of the testing dependencies introduce these files twice, creating a conflict
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/AL2.0'
    }
}

dependencies {
    // Used for java 8 date api for api < 26, since java 8 is not supported before 26
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Library module dependencies
    implementation project(path: ':view')
    implementation project(path: ':styling')
    implementation project(path: ':animalfacts')
    implementation project(path: ':database')


    // View
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    // KTX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.fragment:fragment-ktx:1.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"

    // Room annotation processor can not be implemented through the database module dependency
    kapt "androidx.room:room-compiler:$versions.room"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"


    // Dependency Injection
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    implementation "com.google.dagger:hilt-android:$versions.dagger"
    kapt "com.google.dagger:hilt-compiler:$versions.dagger"

    // Compose
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$versions.compose"
    debugImplementation "androidx.compose.ui:ui-tooling:$versions.compose"
    implementation "androidx.compose.ui:ui:$versions.compose"
    implementation "androidx.compose.material:material:$versions.compose"
    implementation "androidx.compose.ui:ui-tooling-preview:$versions.compose"
    implementation 'androidx.activity:activity-compose:1.4.0'

    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.0-rc02"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0"

    // Test
    testImplementation "junit:junit:$versions.junit"
    testImplementation "io.mockk:mockk:$versions.mockk"

    // Instrumented test
    // To run ui tests, all animations must be deactivated on the testing device:
    // 1: Enable developer mode
    // 2: Go to developer options
    // 3: Scroll down to find "Window-", "Transition-" and "Animator animation scale", and set them all to "Animation off"
    androidTestImplementation "androidx.test.ext:junit:$versions.junitext"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$versions.espresso" // RecyclerView testing
    androidTestImplementation "io.mockk:mockk-android:$versions.mockk"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$versions.dagger"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutines"

    // Dagger kapt's must be included for testing, or annotations won't be processed when running the test application
    kaptAndroidTest "com.google.dagger:dagger-compiler:$versions.dagger"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$versions.dagger"
}
